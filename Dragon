public class Dragon extends Unit{
 
  float dir, speed = 3;  //creating variables for the players location, dimensions, and direction, and speed
  float vx, vy;
  int coolDown = 0;
  
  //setting the players location and dimensions
  public Dragon(float x, float y, float size){
    this.x = x;
    this.y = y;
    this.w = size;
    this.h = size;
    img = loadImage("Dragon.png");
    
    maxHealth = 60;
    health = maxHealth;
  }
  
  public void update(boolean[] keys, float lowestPoint){
    if (keys[1] && vx < speed)  //if d is pressed increase speed to the right
      vx+=0.1;
    else if (keys[3] && vx > -speed)  //if a is pressed increase speed to the left
      vx-=0.1;
    else {
      //slowing down and stopping the dragon if a button isn't pressed
      if (vx > -.1 && vx < .1)
        vx = 0;
      else if (vx < 0)
        vx += 0.1;
      else if (vx > 0)
        vx -= 0.1;
    }
    
    if (keys[0] && vy > -speed)  //if w is pressed speed increased up
      vy-=0.1;
    else if (keys[2] && vy < speed)  //if d is pressed speed increased down
      vy+=0.1;
    else {
      //stopping the dragon if a button isn't pressed
      if (vy > 0 && vy < .2)
        vy = 0.1;
      else if (vy < .1)
        vy += 0.1;
      else if (vy > .1)
        vy -= 0.1;
    }
    
    //updating the players location
    x += vx;
    y += vy;
  
    if (y > lowestPoint){  //stopping the dragons from going below a certain point
      vy = 0;
      y = lowestPoint;
    }
    
    //checking if the player is pressing the attack key
    if (keys[4])
      attack = true;
    else attack = false;
    if (coolDown > 0)  //reducing the players cooldown between attakcs
      coolDown--;
    
    //checking if the player is out of health and removing them if they are
    if (health <= 0)
      Remove();
  }
  
  //creating a new attack from the dragon
  //goes on line 66 of Game  fires.add(player.newAttack(screen.screenX, screen.screenY, screen.scale));
  public Projectile newAttack(float ScreenX, float ScreenY, float scale){
    //firing toward the mouse in relation to the middle of the screen
    float magY = abs(mouseY - (y - ScreenY) * scale);
    float magX = abs(mouseX - (x - ScreenX) * scale);
    float tempY = random(-(magY/4), (magY/4));
    float tempX = random(-(magX/4), (magX/4));
    float dir = atan((mouseY+tempY - magY) / (mouseX+tempX - magX));
    if (mouseX+tempX - magX < 0)
      dir += PI;
    //creating, returning the dragons attack, and setting the players cooldown
    Projectile attack = new DragonFire(x, y, 10, 10, dir);
    coolDown = 2;
    power = power - 10;
    return attack;
  }
  
  //rendering the player for the intro screen
  public void introRender(Screen screen, float y){
    screen.render(img, x, y, w, h, r);
  }
  
  //rendering the players HUD
  public void renderHUD(Screen screen){
    screen.render(1, color(0), width - 50, 20, 80, 20, 0);
    screen.render(1, color(200, 0, 0), width - 10 - 40 * ((float)health / (float)maxHealth), 20, 80 * ((float)health / (float)maxHealth), 20, 0);
  }
  
}
